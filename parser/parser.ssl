%  PT Parser Version 5.0
%
% Date:  30 Jun 1980
%        (Revised v5.0 - 10 Dec 2020)
%
% Author:  Alan Rosselet
%
% Abstract
%     This module parses the syntax token stream emitted by the scanner
%     and emits a postfix stream of tokens to the semantic analyzer
%     module.  The parser detects and repairs syntax errors so that the
%     token stream emitted to the semantic analyzer is syntactically
%     legal (except for the special case of declaration phase errors - 
%     see the comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order

        pOr 'or'
        firstKeywordToken = pOr
        pAnd 'and'
        pNot 'not'
        pThen 'then'
        pElse 'else'
        pOf 'of'
        pEnd 'end'
        % pUntil old PT keyword
        % pDo old PT keyword
        % pArray old PT keyword
        pFile 'file'
        % pProgram old PT keyword
        % pConst old PT keyword
        pVar 'var'
        % pType old PT keyword
        % pProcedure old PT keyword
        % pBegin old PT keyword
        pIf 'if'
        % pCase old PT keyword
        pWhile 'while'
        pRepeat 'repeat'

        % new like keywords
        pChoose 'choose'
        pElseIf 'elseif'
        pFun 'fun'
        pIs 'is'
        pLike 'like'
        pPkg 'pkg'
        pPublic 'public'
        pUsing 'using'
        pVal 'val'
        pWhen 'when'

        lastKeywordToken = pWhen
        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pAlmostEndFile
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        % pColonEquals            ':='
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '<>'
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        % new like tokens
        pSlash '/'
        pPercent '%'
        pHash '#'
        pBang '!'
        pOrBar '|'
        pDoubleOrBar '||'
        pPlusEquals '+='
        pMinusEquals '-='
        pStarEquals '*='
        pSlashEquals '/='
        pPercentEquals '%='
        pDoubleEquals '=='
        
        pDotDot '..'
        lastSyntaxToken = pDotDot;

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral

        sProgram
        sParmBegin
        sParmEnd
        sConst
        % sType old PT token
        % new Like tokens
        sPackage
        sPublic
        sConcatenate
        sRepeatString
        sSubstring
        sLength
        sInitialValue
        sCaseElse
        sLike

        sVar
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sWhileStmt
        sRepeatStmt
        sRepeatEnd
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sNewLine

        sEndOfFile
        lastSemanticToken = sEndOfFile;

Error:
        eUnexpectedToken;

rules

Program : 
        .sProgram
        @UsingStatement
        @Block
        ;
Block:
        .sBegin
        {[
            | pAlmostEndFile: >
            | 'end': ';' >
            | *: @DeclarationOrStatement
        ]}
        .sEnd;

DeclarationOrStatement:
        [
            | pVal : @ConstantDefinitions
            | pVar : @VariableDeclaration
            | pIdentifier: @AssignmentOrCallStmtOrVariableDeclaration
            | pPkg: @Package
            | 'fun': @Procedure
            | 'public': @Public
            | pIf: @IfStatement
            | pRepeat: @RepeatStatement
            | pChoose: @ChooseStatement
            | *:
                ?
                #eUnexpectedToken
        ]
        ;

RepeatBlock: % inside of repeat block where 'while' is at bottom
        .sRepeatStmt
        .sBegin
        {[
            | 'while': >
            | *: @DeclarationOrStatement
        ]}
        .sEnd .sRepeatEnd @Expression .sExpnEnd ';'
        ;

RepeatStatement: % follows 'repeat' keyword
        [
            | 'while': .sWhileStmt @Expression .sExpnEnd @Block
            | *: @RepeatBlock
        ];

IfBlock:
    .sBegin
    {[
        | 'end': ';' .sEnd >
        | 'else': .sEnd .sElse @Block >
        | 'elseif': .sEnd .sElse .sBegin @IfStatement .sEnd >
        | *: @DeclarationOrStatement
    ]};
    

IfStatement:
    .sIfStmt @Expression .sExpnEnd @then @IfBlock
    ;

Procedure:
    .sProcedure 
    @identifier @OptionalProcedureParameters 'is' .sParmEnd @Block;

PublicProcedure:
    .sProcedure 
    @identifier .sPublic @OptionalProcedureParameters 'is' .sParmEnd @Block;

Public:
    [
        | 'val':
            @PublicConstantDefinitions
        | 'var':
            @PublicVariableDeclaration
        | 'fun':
            @PublicProcedure
    ];

Package:
    .sPackage @identifier 'is' @Block;

AssignmentOrCallStmtOrVariableDeclaration: 
	[
		| '(': @CallStatement
		| ';': @CallStatementNoParams
		| *: #eUnexpectedToken
	];

Arguments:
	{[
		| ')': >
		| ',': 
		| *: @ConstantValue
	]};

CallStatementNoParams:
	.sCallStmt .sIdentifier .sParmEnd;

CallStatement: % starts after '('
	.sCallStmt .sIdentifier @Arguments .sExpnEnd .sParmEnd ';'
	;

UsingStatement:
    pUsing @identifier
    {[
        | ',': @identifier
        | ';': >
        | *: #eUnexpectedToken
    ]}
    .sParmEnd
    ;


ConstantDefinition:
    .sConst @identifier '=' @ConstantValue;

PublicConstantDefinition:
    .sConst @identifier .sPublic '=' @ConstantValue;

PublicConstantDefinitions :
        @PublicConstantDefinition
        {[
            | ',': @PublicConstantDefinition % comma separates more definitions
            | ';': >
            | *: #eUnexpectedToken

        ]};

ConstantDefinitions :
        @ConstantDefinition
        {[
            | ',': @ConstantDefinition % comma separates more definitions
            | ';': >
            | *: #eUnexpectedToken
        ]};

ConstantValue :
        [
            | pInteger: .sInteger
            | pStringLiteral: .sStringLiteral
            | pIdentifier: .sIdentifier
            | '-': @UnsignedIntegerConstant .sNegate
            | '+': @UnsignedIntegerConstant
            | *: #eUnexpectedToken
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier: .sIdentifier
            | pInteger: .sInteger
            | *: #eUnexpectedToken
        ];

OptionallySignedIntegerConstant :
        [
            | '-': @UnsignedIntegerConstant .sNegate
            | '+': @UnsignedIntegerConstant
            | *: @UnsignedIntegerConstant
        ];

VariableDeclaration :
        % Accept only one variable declaration
        .sVar @identifier @ValueorLike  ';';

PublicVariableDeclaration :
        % Accept only one variable declaration
        .sVar @identifier .sPublic @ValueorLike  ';';

ParameterIdentifier:
    [
        | 'var': @identifier .sVar
        | pIdentifier: .sIdentifier
        | *: #eUnexpectedToken
    ];

ParameterDeclaration:
    @ParameterIdentifier ':' @LikeClause;

ProcedureParameters:
    @ParameterDeclaration
    {[
        | ')': >
        | ',': @ParameterDeclaration
        | *: #eUnexpectedToken
    ]};

ValueorLike :
        [
            | '=': @Expression
            | '[': .sArray @ConstantValue ']' ':' @LikeClause
            | ':': @LikeClause
        ];

LikeClause:
    @OptionalFile @like @ConstantValue;

ChooseElseBlock :
    .sBegin
    {[
        | pVal : @ConstantDefinitions
        | pVar : @VariableDeclaration
        | pIdentifier: @AssignmentOrCallStmtOrVariableDeclaration
        | pPkg: @Package
        | 'fun': @Procedure
        | 'public': @Public
        | pIf: @IfStatement
        | pRepeat: @RepeatStatement
        | pChoose: @ChooseStatement
        | *:
            >
    ]};

ChooseStatement :
        .sCaseStmt
        @Expression
        .sExpnEnd
        'of'
        @ChooseAlternative
        {[
            | 'when':
                @ChooseAlternative
            | *:
                >
        ]}
        .sCaseEnd
        [
            | 'else':
                .sCaseElse
                @ChooseElseBlock
            | *:
        ]
        pEnd .sEnd ';';

ChooseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants
        'when'
        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        'then'
        .sLabelEnd
        % allow any number of declarations or statements
        @Block;

% VERY SIMPLE RULES BELOW

% lowercase rules indicate simple passthroughs
identifier: pIdentifier .sIdentifier;
like: pLike .sLike;
then: pThen .sThen;

OptionalFile:
    [
        | 'file': .sFile
        | *: 
    ];


OptionalProcedureParameters:
    [
        | '(': @ProcedureParameters
        | *: 
    ];

% 
% Statement :
%         [
%             | pIdentifier:
%                 @AssignmentOrCallStmt
%             | 'begin':
%                 @BeginStmt
%             | 'if':
%                 @IfStmt
%             | 'while':
%                 @WhileStmt
%             | 'case':
%                 @CaseStmt
%             | 'repeat':
%                 @RepeatStmt
%             | *:
%                 .sNullStmt
%         ];
% 
% AssignmentOrCallStmt :
%         [
%             | ':=':
%                 .sAssignmentStmt
%                 .sIdentifier    % identifier accepted in the Statement rule
%                 @Expression
%                 .sExpnEnd
%             | '[':
%                 .sAssignmentStmt
%                 .sIdentifier
%                 .sSubscript
%                 @Expression
%                 .sExpnEnd
%                 ']'  ':='
%                 @Expression
%                 .sExpnEnd
%             | *:
%                 @CallStmt
%         ];
% 
% CallStmt :
%         .sCallStmt
%         .sIdentifier    % procedure identifier accepted in the Statement rule
%         % accept zero or more actual parameters
%         [
%             | '(':
%                 {
%                     @Expression
%                     .sExpnEnd
%                     % allow a field width specification to occur in calls
%                     % to the standard procedures write and writeln
%                     [
%                         | ':':
%                             .sFieldWidth
%                             @Expression
%                             .sExpnEnd
%                         | *:
%                     ]
%                     [
%                         | ',':
%                         | *:
%                             >
%                     ]
%                 }
%                 ')'
%             | *:        % no actual parameters
%         ]
%         .sParmEnd;
% 
% BeginStmt :
%         .sBegin
%         {
%             @Statement
%             [
%                 | ';':
%                 | 'end':
%                     .sEnd
%                     >
%             ]
%         };
% 
% IfStmt :
%         .sIfStmt
%         @Expression
%         .sExpnEnd
%         'then'  .sThen
%         @Statement
%         [
%             | 'else':
%                 .sElse
%                 @Statement
%             | *:
%         ];
% 
% 
% WhileStmt :
%         .sWhileStmt
%         @Expression
%         .sExpnEnd
%         'do'
%         @Statement;
% 
% RepeatStmt :
%         .sRepeatStmt
%         {
%             @Statement
%             [
%                 | ';':
%                 | 'until':
%                     .sRepeatEnd
%                     >
%             ]
%         }
%         @Expression
%         .sExpnEnd;
%

Expression :
        @SimpleExpression
        [
            | '==':
                @SimpleExpression  .sEq
            | '<>':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | *:
                >
        ]};

Term :
        @Factor
        {[
            | '*':
                @Factor  .sMultiply
            | '/':
                @Factor  .sDivide
            | '%':
                @Factor  .sModulus
            | 'and':
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Factor :
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | 'not':
                @Factor
                .sNot
            | pStringLiteral:
                .sStringLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
